buildscript {
    repositories {
        maven { url = "http://maven.dries007.net/content/repositories/ccm/" }
        maven { url = "http://files.minecraftforge.net/maven/" }
        maven { url = "https://libraries.minecraft.net/" }
        mavenCentral()
    }
    
    dependencies {
		classpath 'com.github.abrarsyed.gmcp:GMCP:latest.integration'
    }
}

configurations {
    compile
    deployJars
}

repositories {
    maven { url = "http://maven.dries007.net/content/repositories/ccm/" }
    maven { url = "http://files.minecraftforge.net/maven/" }
    maven { url = "https://libraries.minecraft.net/" }
}

apply plugin: "gmcp"
apply plugin: "maven"

group = "ccm.autoCrafter2000"
version = "1.0.0"

targetCompatibility = 1.6
sourceCompatibility = 1.6

archivesBaseName = 'AutoCrafter2000'
minecraft {
	minecraftVersion = "1.6.4"
}

ext.BUILD_NUMBER = System.getenv().BUILD_NUMBER;
if (ext.BUILD_NUMBER == null) ext.BUILD_NUMBER = "0"
version = project.minecraft.minecraftVersion + "-" + version + "." + ext.BUILD_NUMBER

dependencies {
    compile "ccm.nucleumOmnium:NucleumOmnium:" + project.minecraft.minecraftVersion + "-+:dev"
    compile "com.mod-buildcraft:Buildcraft:1.6.4-+:dev"
    compile fileTree(dir: "libs", includes: ['*.jar'])
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.minecraftVersion, 'nucleumOmniumVersion':configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll({ it.moduleName == "NucleumOmnium"}).collect({ it.moduleVersion})[0].split("-")[1]
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
    appendix = project.minecraft.minecraftVersion
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from (sourceSets.main.allSource.srcDirs) {
        include '**/*.java'
    }
    classifier = 'dev'
    appendix = project.minecraft.minecraftVersion
}

jar {
    appendix = project.minecraft.minecraftVersion
}

artifacts {
	archives jar
    archives sourcesJar
    archives deobfJar
}

uploadArchives {
    if (project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
        repositories {
            mavenDeployer {
                repository(url: "http://maven.dries007.net/content/repositories/ccm/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
                
                pom {
                    groupId = project.group
                    version = project.minecraft.minecraftVersion + "-" + project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Adds a simple automatic crafting table.'
                        url 'https://github.com/CCM-Modding/AutoCrafter2000'

                        scm {
                            url 'https://github.com/CCM-Modding/AutoCrafter2000'
                            connection 'scm:git:git://github.com/CCM-Modding/AutoCrafter2000.git'
                            developerConnection 'scm:git:git@github.com:CCM-Modding/AutoCrafter2000.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/CCM-Modding/AutoCrafter2000/issues'
                        }

                        licenses  {
                            license  {
                                name 'MIT License'
                                url 'https://raw.github.com/CCM-Modding/AutoCrafter2000/master/LICENCE'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'Dries007'
                                name 'Dries007'
                                roles { role 'developer' }
                            }
                            developer {
                                id 'Claycorp'
                                name 'Claycorp'
                                roles { role 'architect' }
                            }
                        }
                    }
                }
            }
        }
    }
}